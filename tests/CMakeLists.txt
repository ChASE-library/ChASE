include(GoogleTest)
find_package( MPI    REQUIRED )

function(setup_test name)
  set(sources)
  set(libraries)
  set(is_library FALSE)
  foreach(arg IN LISTS ARGN)
    if(arg STREQUAL "LIBRARIES")
      set(is_library TRUE)
    elseif(is_library)
      list(APPEND libraries ${arg})
    else()
      list(APPEND sources ${arg})
    endif()
  endforeach()

  add_executable(${name} ${sources} ${CMAKE_SOURCE_DIR}/tests/main.cpp)
  target_include_directories(${name} PRIVATE ${MPI_CXX_INCLUDE_PATH})
  target_link_libraries(${name} ${libraries} ${MPI_CXX_LIBRARIES} GTest::gtest_main)
  
  if(MPI_TEST)
    target_compile_definitions(${name} PRIVATE MPI_NUM_PROCS=4)
    if(${MPI_RUN} STREQUAL "srun")
      add_test(NAME ${name} COMMAND ${MPI_RUN} ${MPI_RUN_ARGS} -n 4 $<TARGET_FILE:${name}>)
    else()#mpirun
      add_test(NAME ${name} COMMAND ${MPI_RUN} ${MPI_RUN_ARGS} -n 1 $<TARGET_FILE:${name}> --gtest_output=xml:unittest_reports/${name}_report.xml : -n 3 $<TARGET_FILE:${name}>)
    endif()
  else()
    target_compile_definitions(${name} PRIVATE MPI_NUM_PROCS=1)
    add_test(NAME ${name} COMMAND ${MPI_RUN} ${MPI_RUN_ARGS} -n 1 $<TARGET_FILE:${name}> --gtest_output=xml:unittest_reports/${name}_report.xml)
  endif()
endfunction()

function(setup_test_serial name)
  set(sources)
  set(libraries)
  set(is_library FALSE)
  foreach(arg IN LISTS ARGN)
    if(arg STREQUAL "LIBRARIES")
      set(is_library TRUE)
    elseif(is_library)
      list(APPEND libraries ${arg})
    else()
      list(APPEND sources ${arg})
    endif()
  endforeach()

  add_executable(${name} ${sources} ${CMAKE_SOURCE_DIR}/tests/main.cpp)
  target_include_directories(${name} PRIVATE ${MPI_CXX_INCLUDE_PATH})
  target_link_libraries(${name} ${libraries} ${MPI_CXX_LIBRARIES} GTest::gtest_main)

  target_compile_definitions(${name} PRIVATE MPI_NUM_PROCS=1 SERIAL_TEST=1)
  add_test(NAME ${name} COMMAND ${MPI_RUN} ${MPI_RUN_ARGS} -n 1 $<TARGET_FILE:${name}> --gtest_output=xml:unittest_reports/${name}_report.xml)
endfunction()

add_subdirectory(linalg)
add_subdirectory(matrix)
